proc('d9995a9995|',getenv('PK_INCREASE_NUMBER'),'|')

if mfn=1 then '['/ fi,

if v6 = 'I' AND left(v607,3) <> 'BRI' AND left(v607,3) <> 'BRE' then,

    /* modelo principal */
   '{',/
       c4,'"pk": ', f(mfn+val(v9995),1,0),','/
       c4,'"model": ','"institution.institution",',/

       c4,'"fields": {',/
            c8,'"status": ',
            if v604:'FECHADO' then '3' else, if v604:'INATIVO' then '2' else '1' fi,fi,','/

            if p(v607) then c8,'"cc_code": "',v607,'",'/, fi,

            if p(v611) then,
                c8,'"name": ','"',v611^a,'",'/
                if p(v611^b) then c8,'"acronym": "',v611^b,'",'/, fi,
            else,
                c8,'"name": ','"',v612^a,'",'/
                if p(v612^b) then c8,'"acronym": "',v612^b,'",'/, fi,
            fi,

            if p(v615) then c8,'"address": "',v615,'",'/, fi,
            if p(v616^a) then c8,'"mailbox": "',v616^a,'",'/, fi,
            if p(v617^a) then c8,'"city": "',v617^a,'",'/, fi,
            if p(v617^b) then c8,'"zipcode": "',v617^b,'",'/, fi,
            if p(v618^a) then c8,'"state": "',v618^a,'",'/, fi,
            if p(v620^a) and val(v620^a) > 0 then c8,'"country": ',f(val(v620^a),1,0),','/, fi,

            c8,'"created_time": ','"2018-04-03T09:00:00-03:00",'/,
            c8,'"cooperative_center_code": ','"BR1.1",'/
            c8,'"created_by": ',getenv('FI_USER_ID'),/

       c4,'}',/
    '},'/

    /* contacts (concatenation of person, email and phone) */
    if p(v610) then,
        '{',/

            c4,'"pk": null,'/
            c4,'"model": ','"institution.contact",'/

            c4,'"fields": {',/
                c8,'"institution": ',f(mfn+val(v9995),1,0),','/
                c8,'"name": "',v610^a,'",'/,
                c8,'"prefix": "',v610^b,'",'/,
                c8,'"job_title": "',v610^c,'"'/,
            c4,'}',/
         '},'/
    fi,
    if p(v624) then,
        (
          '{',/
            c4,'"pk": null,'/
            c4,'"model": ','"institution.contact",'/

            c4,'"fields": {',/
                c8,'"institution": ',f(mfn+val(v9995),1,0),','/
                c8,'"email": "',v624,'"'/,
            c4,'}',/
         '},'/
        ),
    fi,
    if p(v621^*) then,
        '{',/
            c4,'"pk": null,'/
            c4,'"model": ','"institution.contact",'/

            c4,'"fields": {',/
                c8,'"institution": ',f(mfn+val(v9995),1,0),','/
                c8,'"country_area_code": "',v625,'",'/,
                c8,'"phone_number": "',v621^*,'"'/,
            c4,'}',/
         '},'/
    fi,


    /* site */
    if p(v694) then,
        (
          '{',/
            c4,'"pk": null,'/
            c4,'"model": ','"institution.url",'/

            c4,'"fields": {',/
                c8,'"institution": ',f(mfn+val(v9995),1,0),','/
                c8,'"url_type": "main",'/,
                c8,'"url": "',v694,'"'/,
            c4,'}',/
         '},'/
        ),
    fi,

    /* ADM */
    /* gera variaveis com IDs das tabelas controladas de categoria e tipos */
    proc('d6603',(ref(['nmail_cat_list']l(['nmail_cat_list']v603),'a6603|',v2,'|')),),
    proc('d6604',(ref(['nmail_user_list']l(['nmail_user_list']v604),'a6604|',v2,'|')),),

    '{',/
        c4,'"pk": null,'/
        c4,'"model": ','"institution.adm",'/

        c4,'"fields": {',/
            c8,'"institution": ',f(mfn+val(v9995),1,0),','/
            c8,'"category_history": "',(v603+|\r\n|), '",'/,
            c8,'"category": [',(if val(v6603) > 0 then, if iocc > 1 then ', ' fi, v6603 fi),'],',/, 
            c8,'"type_history": "',(v604+|\r\n|),'",'/,
            c8,'"type": [',(if val(v6604) > 0 then, if iocc > 1 then ', ' fi, v6604 fi),'],',/, 
            c8,'"notes": "',(replace(v699,'^a', '\r\n\r\n^a'),'\r\n'),'"'/,
        c4,'}',/
    '},'/

fi,


if mfn=MAXMFN-1 then ']'/ fi,
