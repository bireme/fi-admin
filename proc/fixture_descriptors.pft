proc('d9995a9995|',getenv('PK_INCREASE_NUMBER'),'|')
proc('d9996a9996|',getenv('FI_CONTENT_TYPE_ID'),'|')
proc('d9997a9997|',getenv('FI_USER_ID'),'|')

if mfn=1 then '['/ fi,

if left(v311,11) <> 'IDDUPLICADO' then,

  (
	if p(v323) then,
		proc('d9990<9990 0>',v323,'</9990>')
		proc('Ggansna,9990')
		/* check if is descriptor/qualifier */
		if instr(v323,'/') > 0 then,
			proc('d9991a9991~',left(v9990[1],instr(v323,'/')-1),'~'),
			proc('d9992a9992~',mid(v9990[1],instr(v323,'/'),size(v323)),'~'),
		else,
			proc('d9991d9992a9991~',v9990[1],'~'),
		fi,

		if l(['decs']v9991[1]) <> 0 then,
		   '{',/
				c4,'"model": ','"main.descriptor",',/
				c4,'"pk": ', 'null,'/     	/* avoid PK duplication use auto-increment on load */
				c4,'"fields": {',/
					c8,'"object_id": ',f(mfn+val(v9995[1]),1,0),',',/
					c8,'"content_type": ',v9996[1],',',/
					c8,'"text": ','"', v323, '",'/
					c8,'"code": ','"',ref( l(['decs']v9991[1]),'^d',mfn(0) ), ref( l(['decs']v9992[1]),'^s',mfn(0) ) ,'",'/
					c8,'"status": ','1,'/
                    c8,'"created_time": "2015-05-07T09:00:00-03:00",'/
					c8,'"created_by": ',v9997[1],/
				c4,'}',/
			'},'/
		else,
		   '{',/
				c4,'"model": ','"main.keyword",',/
				c4,'"pk": ', 'null,'/      /* avoid PK duplication use auto-increment on load */
				c4,'"fields": {',/
					c8,'"object_id": ',f(mfn+val(v9995[1]),1,0),',',/
					c8,'"content_type": ', v9996[1],',',/
					c8,'"text": ','"', v323, '",'/
					c8,'"status": ','1,'/
                    c8,'"created_time": "2015-05-07T09:00:00-03:00",'/
					c8,'"created_by": ',v9997[1],/
				c4,'}',/
			'},'/
		fi,
    fi,
  )
fi,
if mfn=MAXMFN-1 then ']'/ fi,
