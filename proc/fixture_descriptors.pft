if mfn=1 then '['/ fi,

if left(v311,11) <> 'IDDUPLICADO' then,

   /* LIS ID (only numeric part) */
   proc('d9301a9301!',mid(v301,instr(v301,'-')+1,size(v301)),'!'),

  (	   
	if p(v323) then,
		proc('d9990<9990 0>',v323,'</9990>')
		proc('Ggansna,9990')
		/* check if is descriptor/qualifier */
		if instr(v323,'/') > 0 then,
			proc('d9991a9991~',left(v9990[1],instr(v323,'/')-1),'~'),
			proc('d9992a9992~',mid(v9990[1],instr(v323,'/'),size(v323)),'~'),
		else,
			proc('d9991d9992a9991~',v9990[1],'~'),
		fi,
		
		if l(['decs']v9991[1]) <> 0 then,
		   '{',/
				c4,'"model": ','"main.descriptor",',/				
				c4,'"pk": ', 'null,'/     	/* avoid PK duplication use auto-increment on load */
				c4,'"fields": {',/
					c8,'"object_id": ',v9301[1],',',/
					c8,'"content_type": ', '15,',/
					c8,'"text": ','"', v323, '",'/
					c8,'"code": ','"',ref( l(['decs']v9991[1]),'^d',mfn(0) ), ref( l(['decs']v9992[1]),'^s',mfn(0) ) ,'",'/
					c8,'"status": ','1,'/
                    c8,'"created_time": "2013-12-01T09:00:00-03:00",'/
					c8,'"created_by": ','2'/
				c4,'}',/
			'},'/
		else,
		   '{',/
				c4,'"model": ','"main.keyword",',/
				c4,'"pk": ', 'null,'/      /* avoid PK duplication use auto-increment on load */
				c4,'"fields": {',/
					c8,'"object_id": ',v9301[1],',',/
					c8,'"content_type": ', '15,',/
					c8,'"text": ','"', v323, '",'/
					c8,'"status": ','1,'/   
                    c8,'"created_time": "2013-12-01T09:00:00-03:00",'/        
					c8,'"created_by": ','2'/
				c4,'}',/
			'},'/
		fi,
    fi,
  )
fi,
if mfn=MAXMFN-1 then ']'/ fi,

