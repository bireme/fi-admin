proc('d9995a9995|',getenv('PK_INCREASE_NUMBER'),'|')
proc('d9996a9996|',getenv('FI_CONTENT_TYPE_ID'),'|')
proc('d9997a9997|',getenv('FI_USER_ID'),'|')


if mfn=1 then '['/ fi,
 (	   
	if p(v600) then,
		/* check if is descriptor/qualifier */
		if instr(v600,'/') > 0 then,
			proc('d9991a9991|',left(v600,instr(v600,'/')-1),'|'),
			proc('d9992a9992|',mid(v600,instr(v600,'/'),size(v600)),'|'),
		else,
			proc('d9991d9992a9991|',v600,'|'),
		fi,
		/* aplica gizmo para retirar acentos */
		proc('G../gizmo/gansna,9991')
		proc('G../gizmo/gansna,9992')

		
		if l(['decs']v9991[1]) <> 0 then,
		   '{',/
				c4,'"model": ','"main.descriptor",',/				
				c4,'"pk": ', 'null,'/     	/* avoid PK duplication use auto-increment on load */
				c4,'"fields": {',/
					c8,'"object_id": ',f(mfn+val(v9995[1]),1,0),',',/
					c8,'"content_type": ', v9996[1],',',/
					c8,'"text": ','"', if size(v600) > 255 then left(v600,255) else v600 fi, '",'/
					c8,'"code": ','"',ref( l(['decs']v9991[1]),'^d',mfn(0) ), ref( l(['decs']v9992[1]),'^s',mfn(0) ) ,'",'/
					c8,'"status": ','1,'/     
					c8,'"created_time": ','"2015-03-12T09:00:00-03:00",'/,
					c8,'"created_by": ',v9997[1],/
				c4,'}',/
			'}'
			if mfn=MAXMFN-1 then if iocc=nocc(v600) then else ',' fi, else ',' fi,/
		else,
		   '{',/
				c4,'"model": ','"main.keyword",',/
				c4,'"pk": ', 'null,'/      /* avoid PK duplication use auto-increment on load */
				c4,'"fields": {',/
					c8,'"object_id": ',f(mfn+val(v9995[1]),1,0),',',/    
					c8,'"content_type": ', v9996[1],',',/
					c8,'"text": ','"', if size(v600) > 255 then left(v600,255) else v600 fi, '",'/
					c8,'"status": ','1,'/           
					c8,'"created_time": ','"2015-03-12T09:00:00-03:00",'/,
					c8,'"created_by": ',v9997[1],/
				c4,'}',/
			'}'			
			if mfn=MAXMFN-1 then if iocc=nocc(v600) then else ',' fi, else ',' fi,/
		fi,
    fi,
)
if mfn=MAXMFN-1 then ']'/ fi,

