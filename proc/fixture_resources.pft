proc('d9995a9995|',getenv('PK_INCREASE_NUMBER'),'|')
proc('d9996a9996|',getenv('FI_CONTENT_TYPE_ID'),'|')
proc('d9997a9997|',getenv('FI_USER_ID'),'|')

if mfn=1 then '['/ fi,

    if p(v311) and left(v311,11) <> 'IDDUPLICADO' then,

        '{',/

           c4,'"pk": ',f(mfn+val(v9995),1,0),','/
           c4,'"model": ','"main.resource",',/

           c4,'"fields": {',/
                c8,'"status": ', v399,',',/
                c8,'"title": ','"',v311,'",'/
                c8,'"link": ','"',(v351+|\r\n|),'",'/
                if p(v313) then c8,'"originator": ','"',(v313+|\r\n|),'",'/, fi,
                c8,'"originator_location": ','[',(if val(v314) > 0 and size(v314) < 4 then, f(val(v314),1,0), if iocc < nocc(v314) then ', ' fi, fi),'],',/
                if p(v315) then c8,'"author": ','"',(v315+|\r\n|),'",'/, fi,
                if p(v317) then c8,'"source_language": ','[',(if val(v317) > 0 then, if iocc > 1 then ', ' fi, v317 fi),'],',/, fi,
                if p(v318) then c8,'"source_type": ','[',(if p(v318) then, if iocc > 1 then ', ' fi, if size(v318) > 3 then '999' else v318 fi,fi),'],',/, fi,
                if p(v319) then c8,'"abstract": ','"',v319,'",'/, fi,
                if p(v341) then c8,'"time_period_textual": ','"',v341,'",'/, fi,
                if p(v361) then c8,'"objective": ','"',v361,'",'/, fi,
                c8,'"cooperative_center_code": ','"',getenv('CC_CODE'),'",'/

                if p(v391) and val(v391[1])>0 then
    				c8,'"created_time": ','"', v391.4'-'v391*4.2'-'v391*6.2 , 'T09:00:00-03:00",'/,
    			else,
    				if p(v393) and val(v393[1])>0 then
    					c8,'"created_time": ','"', v393.4'-'v393*4.2'-'v393*6.2 , 'T09:00:00-03:00",'/,
    				else,
    					c8,'"created_time": ','"2015-05-07T09:00:00-03:00",'/,
    				fi,
    			fi,
                if p(v393) and val(v393[1])>0 then
    				c8,'"updated_time": ','"', v393.4'-'v393*4.2'-'v393*6.2 , 'T09:00:00-03:00",'/,
    			fi,

                c8,'"created_by": ',v9997[1],/
           c4,'}',/
        '}'
        if mfn=MAXMFN-1 then else ',' fi,/
    fi,


    /*
    ADD ERROR REPORTING FOR RESOURCE IF 305 CONTAINS '-'
    if instr(v305,'-')>0 and left(v311,11) <> 'IDDUPLICADO' then,
       '{',/
            c4,'"model": ','"error_reporting.errorreport",',/
            c4,'"pk": ', 'null,'/
            c4,'"fields": {',/
                c8,'"object_id": ',f(mfn+val(v9995),1,0),',',/
                c8,'"content_type": ',v9996[1], ',',/

                c8,'"status": ','1,'/
                c8,'"code": ','3,'/
                c8,'"description": ','"', mid(v305,instr(v305,'-')+2,size(v305)) , '",'/

                c8,'"created_time": "2015-05-07T09:00:00-03:00",'/
                c8,'"created_by": ',v9997[1],/
            c4,'}',/
        '}'
        if mfn=MAXMFN-1 then else ',' fi,/
    fi,
    */

if mfn=MAXMFN-1 then ']'/ fi,
