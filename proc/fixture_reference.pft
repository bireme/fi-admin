
if p(v1) then,
    '{',/

       c4,'"pk": ', v9001[1],','/
       c4,'"model": ','"biblioref.reference",',/

       c4,'"fields": {',/

       /* call number [03] */
       if p(v3) then,
            c8,'"call_number": ', '"[',
            e1:=nocc(v3),
            (,
                '{',
                if v3.1 <> '^' then,
                   '\"text\": \"',v3,
                else,
                    '\"_',v3*1.1,'\": \"',v3*2,
                fi,
                '\"}',
                if iocc < e1 then ', ' fi,
            )
            ']",'
        fi,

       /* database [04] */
       if p(v4) then,
            c8,'"database": ', '"',(v4+|\r\n|),'",'/
       fi,

       /* literature_type [05] */
       if p(v5) then,
            c8,'"literature_type": ', '"',v5,'",'/
       fi,

       /* treatment_level [06] */
       if p(v6) and v9000 = 'ANALYTIC' then,
            c8,'"treatment_level": ','"',v6,'",'/
       fi,

        /* fields populated only for analytics */
       if v9000 = 'ANALYTIC' then,

           /* electronic_address [08] */
           if p(v8) then,
                c8,'"electronic_address": ', '"[',
                e1:=nocc(v8),
                (
                    '{',
                    if v8.1 <> '^' then,
                       '\"text\": \"',v8,
                    else,
                        '\"_',v8*1.1,'\": \"',v8*2,
                    fi,
                    '\"}',
                    if iocc < e1 then ', ' fi,
                )
           fi,
           ']",'

           /* descriptive information [38] */
           if p(v38) then,
                c8,'"descriptive_information": ', '"[',
                e1:=nocc(v38),
                (
                    '{',
                    if v38.1 <> '^' then,
                        '\"text\": \"',
                    fi
                    v38,'\"}',
                    if iocc < e1 then ', ' fi,
                )
                ']",'
           fi,

           /* text language [40] */
           if p(v40) then,
                c8,'"text_language": "',
                e1:=nocc(v40),
                if e1 > 1 then '[' fi,
                (
                  '\"',v40,'\"',
                  if iocc < e1 then ', ' fi,
                )
                if e1 > 1 then ']' fi,
                '", ',
           fi,

           /* internal_note [61] */
           if p(v61) then,
                c8,'"internal_note": ', '"',(v61+|\r\n|),'",'/
           fi,


           /* publication_date [64] */
           if p(v64) then,
              c8,'"publication_date": ', '"',v64[1],'",'/
           fi,

           /* publication_date_normalized [65] */
           if p(v65) then,
              c8,'"publication_date_normalized": ', '"',v65[1],'",'/
           fi,

           /* total_number_of_references [72] */
           if p(v72) then,
              c8,'"total_number_of_references": ', '"',mid(v72,1,100),'",'/
           fi,

           /* time_limits_from [74] */
           if p(v74) then,
              c8,'"time_limits_from": ', '"',v74,'",'/
           fi,

           /* time_limits_to [75] */
           if p(v75) then,
              c8,'"time_limits_to": ', '"',v75,'",'/
           fi,

           /* symbol [78] */
           if p(v78) then,
                c8,'"person_as_subject": ', '"',(v78+|\r\n|),'",'/
           fi,

           /* non_decs_region [82] */
           if p(v82) then,
                c8,'"non_decs_region": ', '"',(v82+|\r\n|),'",'/
           fi,

           /* abstract [83] */
           if p(v83) then,
                c8,'"abstract": ', '"[',
                e1:=nocc(v83),
                (
                    '{',
                    if v83.1 <> '^' then,
                        '\"text\": \"',
                    fi
                    v83,'\"}',
                    if iocc < e1 then ', ' fi,
                )
                ']",'
           fi,

           /* transfer_date_to_database [84] */
           if p(v84) then,
              c8,'"transfer_date_to_database": ', '"',mid(v84,1,20),'",'/
           fi,

           /* author_keyword [85] */
           if p(v85) then,
                c8,'"author_keyword": ', '"[',
                e1:=nocc(v85),
                (
                    '{',
                    if v85.1 <> '^' then,
                        '\"text\": \"',
                    fi
                    v85,'\"}',
                    if iocc < e1 then ', ' fi,
                )
                ']",'
           fi,

           /* item_form [110] */
           if p(v110) then,
              c8,'"item_form": ', '"',mid(v110,1,10),'",'/
           fi,

           /* type_of_computer_file [111] */
           if p(v111) then,
              c8,'"type_of_computer_file": ', '"',mid(v111,1,10),'",'/
           fi,

           /* type_of_cartographic_material [112] */
           if p(v112) then,
              c8,'"type_of_cartographic_material": ', '"',mid(v112,1,10),'",'/
           fi,

           /* type_of_journal [113] */
           if p(v113) then,
              c8,'"type_of_journal": ', '"',mid(v113,1,10),'",'/
           fi,

           /* type_of_visual_material [114] */
           if p(v114) then,
              c8,'"type_of_visual_material": ', '"',mid(v114,1,10),'",'/
           fi,

           /* specific_designation_of_the_material [115] */
           if p(v115) then,
              c8,'"specific_designation_of_the_material": ', '"',mid(v115,1,10),'",'/
           fi,

           /* general_note [500] */
           if p(v500) then,
                c8,'"general_note": ', '"',(v500+|\r\n|),'",'/
           fi,

           /* formatted_contents_note [505] */
           if p(v505) then,
                c8,'"formatted_contents_note": ', '"',(v505+|\r\n|),'",'/
           fi,

           /* additional_physical_form_available_note [530] */
           if p(v530) then,
                c8,'"additional_physical_form_available_note": ', '"',(v530+|\r\n|),'",'/
           fi,

           /* reproduction_note [533] */
           if p(v533) then,
                c8,'"reproduction_note": ', '"',(v533+|\r\n|),'",'/
           fi,

           /* original_version_note [534] */
           if p(v534) then,
                c8,'"original_version_note": ', '"',(v534+|\r\n|),'",'/
           fi,

           /* institution_as_subject [610] */
           if p(v610) then,
                c8,'"institution_as_subject": ', '"',(v610+|\r\n|),'",'/
           fi,

           /* local_descriptors [653] */
           if p(v653) then,
                c8,'"local_descriptors": ', '"',(v653+|\r\n|),'",'/
           fi,

           /* doi_number [724] */
           if p(v724) then,
                c8,'"doi_number": ', '"',mid(v724,1,150),'",'/
           fi,
        fi,

       if p(v93) and val(v93)>0 then
            c8,'"created_time": ','"', v93.4'-'v93*4.2'-'v93*6.2 , 'T09:00:00-03:00",'/,
       else,
            c8,'"created_time": ','"2015-03-01T09:00:00-03:00",'/,
       fi,

       c8,'"LILACS_original_id": ', '"',v2,'",'/
       c8,'"created_by": 2'

       c4,'}',/
    '},'/
fi,
